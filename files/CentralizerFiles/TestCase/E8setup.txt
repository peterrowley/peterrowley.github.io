//Here we construct E_8(2) as a subgroup of GL(248,2), using standard MAGMA commands.

//First we have MAGMA produce E_8(2) as an object in the "GrpLie" category.

H:=GroupOfLieType("E8",GF(2));

//However, when working with E_8(2) we require the natural matrix representation, the adjoint representation.

f:=AdjointRepresentation(H);
Q:=Codomain(f);                 //In this case Q will be GL(248,2);

//We get generators for H by taking elements corresponding to the fundamental roots.

Hgens:=[];
for i:=1 to 8 do
 Append(~Hgens,elt<H|<i,1>>);
end for;
for i:=1 to 8 do
 Append(~Hgens,elt<H|<120+i,1>>);
end for;

//Now we map them into the matrix group.

Ggens:=[];
for h in Hgens do
 Append(~Ggens,f(h));
end for;

//And now we can construct E_8(2) as a subgroup of Q.

G:=sub<Q|Ggens>;
