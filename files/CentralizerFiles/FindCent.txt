procedure FindCent(G,g,H,l,k,m,e)

//Here G is the matrix group you are working in. G should be finite-dimensional, defined over a finite field GF(q). g is the element whose centralizer you are looking for, and H is a subgroup of G which is isomorphic to the centraliser in G of g. The parameter k will typically be a small positive integer; however it needs to be at least as large as the dimension of the smallest non-trivial irreducible <g>-module over GF(q). Parameters l, m and e should be positive integers, with l > 1.

//First decompose the module under H.

d:=Degree(G);
q:=#CoefficientRing(G);
Q:=GL(d,q);

U:=GModule(H);
CompsU:=CompositionFactors(U);
dimsU:={};
for c in CompsU do
 Include(~dimsU,Dimension(c));
end for;

Cg:=sub<Q|Id(Q)>;
count:=0;

repeat

//Search randomly for an involution which generates a `not too big' subgroup along with g.

 repeat
  val,t:=RandomElementOfOrder(G,2);
  Y:=sub<Q|t,g>;
  W:=GModule(Y);
  CFW:=CompositionFactors(W);
 until #CFW ge l;

 print "Found a suitable involution t";

//Try to find small enough subgroups of Y in which we can construct the centralizer of g.

 for i:=1 to 5 do

  flag:=0;
  counter:=0;
  repeat
   val,a:=RandomElementOfOrder(Y,2);
   L:=sub<Q|a,g>;
   X:=GModule(L);
   CFX:=CompositionFactors(X);
   dims:={};
   for c in CFX do
    Include(~dims,Dimension(c));
   end for;
   counter:=counter+1;
 
   if Max(dims) le k then
    flag:=1;
   end if;
  until Max(dims) le k or counter eq m;

  if flag eq 1 then
   Lsize:=LMGOrder(L); Factorisation(Lsize);

   if Lsize le 2^e then
    CL:=Centralizer(L,g); //Could adjust to look for normalizers at this point.
    Cg:=sub<Q|Cg,CL>;
    else print "Couldn't find small enough subgroup to use, trying again";
   end if;
  
  end if;
 end for;

 VCg:=GModule(Cg);
 count:=count+1;
 count;
 CFVCg:=CompositionFactors(VCg);
 dimsVCg:={};
 for c in CFVCg do
  Include(~dimsVCg,Dimension(c));
 end for;

 if #CFVCg le 30 then
  print "Composition factors of GModule of centralizer subgroup are";
  CFVCg;
  else print "GModule of centralizer subgroup has more than 30 composition factors";
 end if;

until dimsU eq dimsVCg;

//We stop when the subgroup of C_G(g) we have produced acts in the same way as H on the G-module. This does not necessarily guarantee that this subgroup is isomorphic to H, but usually gives a good indication of whether the whole centralizer has been produced. Depending on the context, further checks may be carried out to confirm that the whole centralizer has been produced.

end procedure; 
